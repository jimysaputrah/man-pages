<!DOCTYPE html>
<html>
    <head>
        <meta name="description" content="This Moose cookbook recipe shows how operator overloading, coercion,..." />
        <meta http-equiv="Content-Type" CONTENT="text/html; charset="utf-8" />
        <script type="text/javascript" src="../js/ga.js"></script>
        <title>Moose::Cookbook::Basics::Recipe9 - Operator overloading, subtypes, and</title>
        <META NAME="robots" CONTENT="all" />
    </head>
    
<content><pre>

Moose::Cookbook::BasicUsereContributed PerlMoose::Cookbook::Basics::Recipe9(3)



NAME
       Moose::Cookbook::Basics::Recipe9 - Operator overloading, subtypes, and
       coercion

SYNOPSIS
	 package Human;

	 use Moose;
	 use Moose::Util::TypeConstraints;

	 subtype 'Gender'
	     =&gt as 'Str'
	     =&gt where { $_ =~ m{^[mf]$}s };

	 has 'gender' =&gt ( is =&gt 'ro', isa =&gt 'Gender', required =&gt 1 );

	 has 'mother' =&gt ( is =&gt 'ro', isa =&gt 'Human' );
	 has 'father' =&gt ( is =&gt 'ro', isa =&gt 'Human' );

	 use overload '+' =&gt \&_overload_add, fallback =&gt 1;

	 sub _overload_add {
	     my ( $one, $two ) = @_;

	     die('Only male and female humans may create children')
		 if ( $one-&gtgender() eq $two-&gtgender() );

	     my ( $mother, $father )
		 = ( $one-&gtgender eq 'f' ? ( $one, $two ) : ( $two, $one ) );

	     my $gender = 'f';
	     $gender = 'm' if ( rand() &gt= 0.5 );

	     return Human-&gtnew(
		 gender =&gt $gender,
		 mother =&gt $mother,
		 father =&gt $father,
	     );
	 }

DESCRIPTION
       This Moose cookbook recipe shows how operator overloading, coercion,
       and sub types can be used to mimic the human reproductive system (well,
       the selection of genes at least).

INTRODUCTION
       Our "Human" class uses operator overloading to allow us to "add" two
       humans together and produce a child. Our implementation does require
       that the two objects be of opposite genders. Remember, we're talking
       about biological reproduction, not marriage.

       While this example works as-is, we can take it a lot further by adding
       genes into the mix. We'll add the two genes that control eye color, and
       use overloading to combine the genes from the parent to model the
       biology.

   What is Operator Overloading?
       Overloading is not a Moose-specific feature. It's a general OO concept
       that is implemented in Perl with the "overload" pragma. Overloading
       lets objects do something sane when used with Perl's built in
       operators, like addition ("+") or when used as a string.

       In this example we overload addition so we can write code like "$child
       = $mother + $father".

GENES
       There are many genes which affect eye color, but there are two which
       are most important, gey and bey2. We will start by making a class for
       each gene.

   Human::Gene::bey2
	 package Human::Gene::bey2;

	 use Moose;
	 use Moose::Util::TypeConstraints;

	 type 'bey2_color' =&gt where { $_ =~ m{^(?:brown|blue)$} };

	 has 'color' =&gt ( is =&gt 'ro', isa =&gt 'bey2_color' );

       This class is trivial, We have a type constraint for the allowed
       colors, and a "color" attribute.

   Human::Gene::gey
	 package Human::Gene::gey;

	 use Moose;
	 use Moose::Util::TypeConstraints;

	 type 'gey_color' =&gt where { $_ =~ m{^(?:green|blue)$} };

	 has 'color' =&gt ( is =&gt 'ro', isa =&gt 'gey_color' );

       This is nearly identical to the "Humane::Gene::bey2" class, except that
       the gey gene allows for different colors.

EYE COLOR
       We could just give add four attributes (two of each gene) to the
       "Human" class, but this is a bit messy. Instead, we'll abstract the
       genes into a container class, "Human::EyeColor". Then a "Human" can
       have a single "eye_color" attribute.

	 package Human::EyeColor;

	 use Moose;
	 use Moose::Util::TypeConstraints;

	 coerce 'Human::Gene::bey2'
	     =&gt from 'Str'
		 =&gt via { Human::Gene::bey2-&gtnew( color =&gt $_ ) };

	 coerce 'Human::Gene::gey'
	     =&gt from 'Str'
		 =&gt via { Human::Gene::gey-&gtnew( color =&gt $_ ) };

	 has [qw( bey2_1 bey2_2 )] =&gt
	     ( is =&gt 'ro', isa =&gt 'Human::Gene::bey2', coerce =&gt 1 );

	 has [qw( gey_1 gey_2 )] =&gt
	     ( is =&gt 'ro', isa =&gt 'Human::Gene::gey', coerce =&gt 1 );

       The eye color class has two of each type of gene. We've also created a
       coercion for each class that coerces a string into a new object. Note
       that a coercion will fail if it attempts to coerce a string like
       "indigo", because that is not a valid color for either type of gene.

       As an aside, you can see that we can define several identical
       attributes at once by supply an array reference of names as the first
       argument to "has".

       We also need a method to calculate the actual eye color that results
       from a set of genes. The bey2 brown gene is dominant over both blue and
       green. The gey green gene dominant over blue.

	 sub color {
	     my ($self) = @_;

	     return 'brown'
		 if ( $self-&gtbey2_1-&gtcolor() eq 'brown'
		 or $self-&gtbey2_2-&gtcolor() eq 'brown' );

	     return 'green'
		 if ( $self-&gtgey_1-&gtcolor() eq 'green'
		 or $self-&gtgey_2-&gtcolor() eq 'green' );

	     return 'blue';
	 }

       We'd like to be able to treat a "Human::EyeColor" object as a string,
       so we define a string overloading for the class:

	 use overload '""' =&gt \&color, fallback =&gt 1;

       Finally, we need to define overloading for addition. That way we can
       add together to "Human::EyeColor" objects and get a new one with a new
       (genetically correct) eye color.

	 use overload '+' =&gt \&_overload_add, fallback =&gt 1;

	 sub _overload_add {
	     my ( $one, $two ) = @_;

	     my $one_bey2 = 'bey2_' . _rand2();
	     my $two_bey2 = 'bey2_' . _rand2();

	     my $one_gey = 'gey_' . _rand2();
	     my $two_gey = 'gey_' . _rand2();

	     return Human::EyeColor-&gtnew(
		 bey2_1 =&gt $one-&gt$one_bey2-&gtcolor(),
		 bey2_2 =&gt $two-&gt$two_bey2-&gtcolor(),
		 gey_1	=&gt $one-&gt$one_gey-&gtcolor(),
		 gey_2	=&gt $two-&gt$two_gey-&gtcolor(),
	     );
	 }

	 sub _rand2 {
	     return 1 + int( rand(2) );
	 }

       When two eye color objects are added together the "_overload_add()"
       method will be passed two "Human::EyeColor" objects. These are the left
       and right side operands for the "+" operator. This method returns a new
       "Human::EyeColor" object.

ADDING EYE COLOR TO "Human"s
       Our original "Human" class requires just a few changes to incorporate
       our new "Human::EyeColor" class.

	 use List::MoreUtils qw( zip );

	 coerce 'Human::EyeColor'
	     =&gt from 'ArrayRef'
	     =&gt via { my @genes = qw( bey2_1 bey2_2 gey_1 gey_2 );
		      return Human::EyeColor-&gtnew( zip( @genes, @{$_} ) ); };

	 has 'eye_color' =&gt (
	     is       =&gt 'ro',
	     isa      =&gt 'Human::EyeColor',
	     coerce   =&gt 1,
	     required =&gt 1,
	 );

       We also need to modify "_overload_add()" in the "Human" class to
       account for eye color:

	 return Human-&gtnew(
	     gender    =&gt $gender,
	     eye_color =&gt ( $one-&gteye_color() + $two-&gteye_color() ),
	     mother    =&gt $mother,
	     father    =&gt $father,
	 );

CONCLUSION
       The three techniques we used, overloading, subtypes, and coercion,
       combine to provide a powerful interface.

       If you'd like to learn more about overloading, please read the
       documentation for the overload pragma.

       To see all the code we created together, take a look at
       t/000_recipes/basics/010_genes.t.

NEXT STEPS
       Had this been a real project we'd probably want:

       Better Randomization with Crypt::Random
       Characteristic Base Class
       Mutating Genes
       More Characteristics
       Artificial Life

AUTHORS
       Aran Clary Deltac &ltbluefeet@cpan.org&gt

       Dave Rolsky &ltautarch@urth.org&gt

LICENSE
       This work is licensed under a Creative Commons Attribution 3.0 Unported
       License.

       License details are at: &lthttp://creativecommons.org/licenses/by/3.0/&gt



perl v5.10.1			  2010-02-2Moose::Cookbook::Basics::Recipe9(3)
    </pre></body>

</html>

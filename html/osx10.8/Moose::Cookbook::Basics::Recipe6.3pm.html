<!DOCTYPE html>
<html>
    <head>
        <meta name="description" content="This recipe shows how the "augment" method modifier works. This..." />
        <meta http-equiv="Content-Type" CONTENT="text/html; charset="utf-8" />
        <script type="text/javascript" src="../js/ga.js"></script>
        <title>Moose::Cookbook::Basics::Recipe6 - The augment/inner example</title>
        <META NAME="robots" CONTENT="all" />
    </head>
    
<content><pre>

Moose::Cookbook::BasicUsereContributed PerlMoose::Cookbook::Basics::Recipe6(3)



NAME
       Moose::Cookbook::Basics::Recipe6 - The augment/inner example

VERSION
       version 2.0205

SYNOPSIS
	 package Document::Page;
	 use Moose;

	 has 'body' =&gt ( is =&gt 'rw', isa =&gt 'Str', default =&gt sub {''} );

	 sub create {
	     my $self = shift;
	     $self-&gtopen_page;
	     inner();
	     $self-&gtclose_page;
	 }

	 sub append_body {
	     my ( $self, $appendage ) = @_;
	     $self-&gtbody( $self-&gtbody . $appendage );
	 }

	 sub open_page	{ (shift)-&gtappend_body('&ltpage&gt') }
	 sub close_page { (shift)-&gtappend_body('&lt/page&gt') }

	 package Document::PageWithHeadersAndFooters;
	 use Moose;

	 extends 'Document::Page';

	 augment 'create' =&gt sub {
	     my $self = shift;
	     $self-&gtcreate_header;
	     inner();
	     $self-&gtcreate_footer;
	 };

	 sub create_header { (shift)-&gtappend_body('&ltheader/&gt') }
	 sub create_footer { (shift)-&gtappend_body('&ltfooter/&gt') }

	 package TPSReport;
	 use Moose;

	 extends 'Document::PageWithHeadersAndFooters';

	 augment 'create' =&gt sub {
	     my $self = shift;
	     $self-&gtcreate_tps_report;
	     inner();
	 };

	 sub create_tps_report {
	     (shift)-&gtappend_body('&ltreport type="tps"/&gt');
	 }

	 # &ltpage&gt&ltheader/&gt&ltreport type="tps"/&gt&ltfooter/&gt&lt/page&gt
	 my $report_xml = TPSReport-&gtnew-&gtcreate;

DESCRIPTION
       This recipe shows how the "augment" method modifier works. This
       modifier reverses the normal subclass to parent method resolution
       order. With an "augment" modifier the least specific method is called
       first. Each successive call to "inner" descends the inheritance tree,
       ending at the most specific subclass.

       The "augment" modifier lets you design a parent class that can be
       extended in a specific way. The parent provides generic wrapper
       functionality, and the subclasses fill in the details.

       In the example above, we've created a set of document classes, with the
       most specific being the "TPSReport" class.

       We start with the least specific class, "Document::Page". Its create
       method contains a call to "inner()":

	 sub create {
	     my $self = shift;
	     $self-&gtopen_page;
	     inner();
	     $self-&gtclose_page;
	 }

       The "inner" function is exported by "Moose", and is like "super" for
       augmented methods. When "inner" is called, Moose finds the next method
       in the chain, which is the "augment" modifier in
       "Document::PageWithHeadersAndFooters". You'll note that we can call
       "inner" in our modifier:

	 augment 'create' =&gt sub {
	     my $self = shift;
	     $self-&gtcreate_header;
	     inner();
	     $self-&gtcreate_footer;
	 };

       This finds the next most specific modifier, in the "TPSReport" class.

       Finally, in the "TPSReport" class, the chain comes to an end:

	 augment 'create' =&gt sub {
	     my $self = shift;
	     $self-&gtcreate_tps_report;
	     inner();
	 };

       We do call the "inner" function one more time, but since there is no
       more specific subclass, this is a no-op. Making this call means we can
       easily subclass "TPSReport" in the future.

CONCLUSION
       The "augment" modifier is a powerful tool for creating a set of nested
       wrappers. It's not something you will need often, but when you do, it
       is very handy.

AUTHOR
       Stevan Little &ltstevan@iinteractive.com&gt

COPYRIGHT AND LICENSE
       This software is copyright (c) 2011 by Infinity Interactive, Inc..

       This is free software; you can redistribute it and/or modify it under
       the same terms as the Perl 5 programming language system itself.



perl v5.12.4			  2011-09-0Moose::Cookbook::Basics::Recipe6(3)
    </pre></body>

</html>

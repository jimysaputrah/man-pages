<!DOCTYPE html>
<html>
    <head>
        <meta name="description" content=""aliased" is simple in concept but is a rather handy module.  It loads..." />
        <meta http-equiv="Content-Type" CONTENT="text/html; charset="utf-8" />
        <title>aliased - Use shorter versions of class names.</title>
        <META NAME="robots" CONTENT="all" />
    </head>
    
<body><pre>
aliased(3)	      User Contributed Perl Documentation	    aliased(3)



NAME
       aliased - Use shorter versions of class names.

VERSION
       0.30

SYNOPSIS
	 # Class name interface
	 use aliased 'My::Company::Namespace::Customer';
	 my $cust = Customer->new;

	 use aliased 'My::Company::Namespace::Preferred::Customer' => 'Preferred';
	 my $pref = Preferred->new;


	 # Variable interface
	 use aliased;
	 my $Customer  = alias "My::Other::Namespace::Customer";
	 my $cust      = $Customer->new;

	 my $Preferred = alias "My::Other::Namespace::Preferred::Customer";
	 my $pref      = $Preferred->new;

DESCRIPTION
       "aliased" is simple in concept but is a rather handy module.  It loads
       the class you specify and exports into your namespace a subroutine that
       returns the class name.	You can explicitly alias the class to another
       name or, if you prefer, you can do so implicitly.  In the latter case,
       the name of the subroutine is the last part of the class name.  Thus,
       it does something similar to the following:

	 #use aliased 'Some::Annoyingly::Long::Module::Name::Customer';

	 use Some::Annoyingly::Long::Module::Name::Customer;
	 sub Customer {
	   return 'Some::Annoyingly::Long::Module::Name::Customer';
	 }
	 my $cust = Customer->new;

       This module is useful if you prefer a shorter name for a class.	It's
       also handy if a class has been renamed.

       (Some may object to the term "aliasing" because we're not aliasing one
       namespace to another, but it's a handy term.  Just keep in mind that
       this is done with a subroutine and not with typeglobs and weird
       namespace munging.)

       Note that this is only for "use"ing OO modules.	You cannot use this to
       load procedural modules.  See the Why OO Only? section.	Also, don't
       let the version number fool you.  This code is ridiculously simple and
       is just fine for most use.

   Implicit Aliasing
       The most common use of this module is:

	 use aliased 'Some::Module::name';

       "aliased" will  allow you to reference the class by the last part of
       the class name.	Thus, "Really::Long::Name" becomes "Name".  It does
       this by exporting a subroutine into your namespace with the same name
       as the aliased name.  This subroutine returns the original class name.

       For example:

	 use aliased "Acme::Company::Customer";
	 my $cust = Customer->find($id);

       Note that any class method can be called on the shorter version of the
       class name, not just the constructor.

   Explicit Aliasing
       Sometimes two class names can cause a conflict (they both end with
       "Customer" for example), or you already have a subroutine with the same
       name as the aliased name.  In that case, you can make an explicit alias
       by stating the name you wish to alias to:

	 use aliased 'Original::Module::Name' => 'NewName';

       Here's how we use "aliased" to avoid conflicts:

	 use aliased "Really::Long::Name";
	 use aliased "Another::Really::Long::Name" => "Aname";
	 my $name  = Name->new;
	 my $aname = Aname->new;

       You can even alias to a different package:

	 use aliased "Another::Really::Long::Name" => "Another::Name";
	 my $aname = Another::Name->new;

       Messing around with different namespaces is a really bad idea and you
       probably don't want to do this.	However, it might prove handy if the
       module you are using has been renamed.  If the interface has not
       changed, this allows you to use the new module by only changing one
       line of code.

	 use aliased "New::Module::Name" => "Old::Module::Name";
	 my $thing = Old::Module::Name->new;

   Import Lists
       Sometimes, even with an OO module, you need to specify extra arguments
       when using the module.  When this happens, simply use "Explicit
       Aliasing" followed by the import list:

       Snippet 1:

	 use Some::Module::Name qw/foo bar/;
	 my $o = Some::Module::Name->some_class_method;

       Snippet 2 (equivalent to snippet 1):

	 use aliased 'Some::Module::Name' => 'Name', qw/foo bar/;
	 my $o = Name->some_class_method;

       Note:  remember, you cannot use import lists with "Implicit Aliasing".
       As a result, you may simply prefer to only use "Explicit Aliasing" as a
       matter of style.

   alias()
	   my $alias = alias($class);
	   my $alias = alias($class, @imports);

       alias() is an alternative to "use aliased ..." which uses less magic
       and avoids some of the ambiguities.

       Like "use aliased" it "use"s the $class (pass in @imports, if given)
       but instead of providing an "Alias" constant it simply returns a scalar
       set to the $class name.

	   my $thing = alias("Some::Thing::With::A::Long::Name");

	   # Just like Some::Thing::With::A::Long::Name->method
	   $thing->method;

       The use of a scalar instead of a constant avoids any possible ambiguity
       when aliasing two similar names:

	   # No ambiguity despite the fact that they both end with "Name"
	   my $thing = alias("Some::Thing::With::A::Long::Name");
	   my $other = alias("Some::Other::Thing::With::A::Long::Name");

       and there is no magic constant exported into your namespace.

       The only caveat is loading of the $class happens at run time.  If
       $class exports anything you might want to ensure it is loaded at
       compile time with:

	   my $thing;
	   BEGIN { $thing = alias("Some::Thing"); }

       However, since OO classes rarely export this should not be necessary.

   Why OO Only?
       Some people have asked why this code only support object-oriented
       modules (OO).  If I were to support normal subroutines, I would have to
       allow the following syntax:

	 use aliased 'Some::Really::Long::Module::Name';
	 my $data = Name::data();

       That causes a serious problem.  The only (reasonable) way it can be
       done is to handle the aliasing via typeglobs.  Thus, instead of a
       subroutine that provides the class name, we alias one package to
       another (as the namespace module does.)	However, we really don't want
       to simply alias one package to another and wipe out namespaces willy-
       nilly.  By merely exporting a single subroutine to a namespace, we
       minimize the issue.

       Fortunately, this doesn't seem to be that much of a problem.  Non-OO
       modules generally support exporting of the functions you need and this
       eliminates the need for a module such as this.

EXPORT
       This modules exports a subroutine with the same name as the "aliased"
       name.

BUGS
       There are no known bugs in this module, but feel free to email me
       reports.

SEE ALSO
       The namespace module.

THANKS
       Many thanks to Rentrak, Inc. (http://www.rentrak.com/) for graciously
       allowing me to replicate the functionality of some of their internal
       code.

AUTHOR
       Curtis Poe, "ovid [at] cpan [dot] org"

COPYRIGHT AND LICENSE
       Copyright (C) 2005 by Curtis "Ovid" Poe

       This library is free software; you can redistribute it and/or modify it
       under the same terms as Perl itself, either Perl version 5.8.5 or, at
       your option, any later version of Perl 5 you may have available.



perl v5.12.4			  2012-06-20			    aliased(3)
    </pre><script type="text/javascript" src="../js/ga.js"></script></body>

</html>

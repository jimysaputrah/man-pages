<!DOCTYPE html>
<html>
    <head>
        <meta name="description" content="The autobox pragma allows methods to be called on integers, floats,..." />
        <meta http-equiv="Content-Type" CONTENT="text/html; charset="utf-8" />
        <script type="text/javascript" src="../js/ga.js"></script>
        <title>autobox - call methods on native types</title>
        <META NAME="robots" CONTENT="all" />
    </head>
    
<content><pre>

autobox(3)	      User Contributed Perl Documentation	    autobox(3)



NAME
       autobox - call methods on native types

SYNOPSIS
	   use autobox;

	   # integers

	       my $range = 10-&gtto(1); # [ 10, 9, 8, 7, 6, 5, 4, 3, 2, 1 ]

	   # floats

	       my $error = 3.1415927-&gtminus(22/7)-&gtabs();

	   # strings

	       my @list = 'SELECT * FROM foo'-&gtlist();
	       my $greeting = "Hello, world!"-&gtupper(); # "HELLO, WORLD!"

	       $greeting-&gtfor_each(\&character_handler);

	   # arrays and array refs

	       my $schwartzian = @_-&gtmap(...)-&gtsort(...)-&gtmap(...);
	       my $hash = [ 'SELECT * FROM foo WHERE id IN (?, ?)', 1, 2 ]-&gthash();

	   # hashes and hash refs

	       { alpha =&gt 'beta', gamma =&gt 'vlissides' }-&gtfor_each(...);
	       %hash-&gtkeys();

	   # code refs

	       my $plus_five = (\&add)-&gtcurry()-&gt(5);
	       my $minus_three = sub { $_[0] - $_[1] }-&gtreverse-&gtcurry-&gt(3);

	   # can, isa, VERSION, import and unimport can be accessed via autobox_class

	       42-&gtautobox_class-&gtisa('MyNumber')
	       say []-&gtautobox_class-&gtVERSION

DESCRIPTION
       The autobox pragma allows methods to be called on integers, floats,
       strings, arrays, hashes, and code references in exactly the same manner
       as blessed references.

       The autoboxing is transparent: boxed values are not blessed into their
       (user-defined) implementation class (unless the method elects to bestow
       such a blessing) - they simply use its methods as though they are.

       The classes (packages) into which the native types are boxed are fully
       configurable.  By default, a method invoked on a non-object is assumed
       to be defined in a class whose name corresponds to the "ref()" type of
       that value - or SCALAR if the value is a non-reference.

       This mapping can be overridden by passing key/value pairs to the "use
       autobox" statement, in which the keys represent native types, and the
       values their associated classes.

       As with regular objects, autoboxed values are passed as the first
       argument of the specified method.  Consequently, given a vanilla "use
       autobox":

	   "Hello, world!"-&gtupper()

       is invoked as:

	   SCALAR::upper("hello, world!")

       while:

	   [ 1 .. 10 ]-&gtfor_each(sub { ... })

       resolves to:

	   ARRAY::for_each([ 1 .. 10 ], sub { ... })

       Values beginning with the array "@" and hash "%" sigils are passed by
       reference, i.e. under the default bindings:

	   @array-&gtjoin(', ')
	   @{ ... }-&gtlength()
	   %hash-&gtkeys()
	   %$hash-&gtvalues()

       are equivalent to:

	   ARRAY::join(\@array, ', ')
	   ARRAY::length(\@{ ... })
	   HASH::keys(\%hash)
	   HASH::values(\%$hash)

       Multiple "use autobox" statements can appear in the same scope. These
       are merged both "horizontally" (i.e.  multiple classes can be
       associated with a particular type) and "vertically" (i.e. multiple
       classes can be associated with multiple types).

       Thus:

	   use autobox SCALAR =&gt 'Foo';
	   use autobox SCALAR =&gt 'Bar';

       - associates SCALAR types with a synthetic class whose @ISA includes
       both "Foo" and "Bar" (in that order).

       Likewise:

	   use autobox SCALAR =&gt 'Foo';
	   use autobox SCALAR =&gt 'Bar';
	   use autobox ARRAY  =&gt 'Baz';

       and

	   use autobox SCALAR =&gt [ 'Foo', 'Bar' ];
	   use autobox ARRAY  =&gt 'Baz';

       - bind SCALAR types to the "Foo" and "Bar" classes and ARRAY types to
       "Baz".

       "autobox" is lexically scoped, and bindings for an outer scope can be
       extended or countermanded in a nested scope:

	   {
	       use autobox; # default bindings: autobox all native types
	       ...

	       {
		   # appends 'MyScalar' to the @ISA associated with SCALAR types
		   use autobox SCALAR =&gt 'MyScalar';
		   ...
	       }

	       # back to the default (no MyScalar)
	       ...
	   }

       Autoboxing can be turned off entirely by using the "no" syntax:

	   {
	       use autobox;
	       ...
	       no autobox;
	       ...
	   }

       - or can be selectively disabled by passing arguments to the "no
       autobox" statement:

	   use autobox; # default bindings

	   no autobox qw(SCALAR);

	   []-&gtfoo(); # OK: ARRAY::foo([])

	   "Hello, world!"-&gtbar(); # runtime error

       Autoboxing is not performed for barewords i.e.

	   my $foo = Foo-&gtnew();

       and:

	   my $foo = new Foo;

       behave as expected.

       Methods are called on native types by means of the arrow operator. As
       with regular objects, the right hand side of the operator can either be
       a bare method name or a variable containing a method name or subroutine
       reference. Thus the following are all valid:

	   sub method1 { ... }
	   my $method2 = 'some_method';
	   my $method3 = sub { ... };
	   my $method4 = \&some_method;

	   " ... "-&gtmethod1();
	   [ ... ]-&gt$method2();
	   { ... }-&gt$method3();
	   sub { ... }-&gt$method4();

       A native type is only asociated with a class if the type =&gt class
       mapping is supplied in the "use autobox" statement. Thus the following
       will not work:

	   use autobox SCALAR =&gt 'MyScalar';

	   @array-&gtsome_array_method();

       - as no class is specified for the ARRAY type. Note: the result of
       calling a method on a native type that is not associated with a class
       is the usual runtime error message:

	   Can't call method "some_array_method" on unblessed reference at ...

       As a convenience, there is one exception to this rule. If "use autobox"
       is invoked with no arguments (ignoring the DEBUG option) the four main
       native types are associated with classes of the same name.

       Thus:

	   use autobox;

       - is equivalent to:

	   use autobox
	       SCALAR =&gt 'SCALAR',
	       ARRAY  =&gt 'ARRAY',
	       HASH   =&gt 'HASH',
	       CODE   =&gt 'CODE';

       This facilitates one-liners and prototypes:

	   use autobox;

	   sub SCALAR::split { [ split '', $_[0] ] }
	   sub ARRAY::length { scalar @{$_[0]} }

	   print "Hello, world!"-&gtsplit-&gtlength();

       However, using these default bindings is not recommended as there's no
       guarantee that another piece of code won't trample over the same
       namespace/methods.

OPTIONS
       A mapping from native types to their user-defined classes can be
       specified by passing a list of key/value pairs to the "use autobox"
       statement.

       The following example shows the range of valid arguments:

	   use autobox
	       SCALAR	 =&gt 'MyScalar'			   # class name
	       ARRAY	 =&gt 'MyNamespace::',		   # class prefix (ending in '::')
	       HASH	 =&gt [ 'MyHash', 'MyNamespace::' ], # one or more class names and/or prefixes
	       CODE	 =&gt ...,			   # any of the 3 value types above
	       INTEGER	 =&gt ...,			   # any of the 3 value types above
	       FLOAT	 =&gt ...,			   # any of the 3 value types above
	       NUMBER	 =&gt ...,			   # any of the 3 value types above
	       STRING	 =&gt ...,			   # any of the 3 value types above
	       UNDEF	 =&gt ...,			   # any of the 3 value types above
	       UNIVERSAL =&gt ...,			   # any of the 3 value types above
	       DEFAULT	 =&gt ...,			   # any of the 3 value types above
	       DEBUG	 =&gt ...;			   # boolean or coderef

       The INTEGER, FLOAT, NUMBER, STRING, SCALAR, ARRAY, HASH, CODE, UNDEF,
       DEFAULT and UNIVERSAL options can take three different types of value:

       o   A class name e.g.

	       use autobox INTEGER =&gt 'MyInt';

	   This binds the specified native type to the specified class. All
	   methods invoked on literals or values of type "key" will be
	   dispatched as methods of the class specified in the corresponding
	   "value".

       o   A namespace: this is a class prefix (up to and including the final
	   '::') to which the specified type name (INTEGER, FLOAT, STRING &c.)
	   will be appended:

	   Thus:

	       use autobox ARRAY =&gt 'Prelude::';

	   is equivalent to:

	       use autobox ARRAY =&gt 'Prelude::ARRAY';

       o   A reference to an array of class names and/or namespaces. This
	   associates multiple classes with the specified type.

   DEFAULT
       The "DEFAULT" option specifies bindings for any of the four default
       types (SCALAR, ARRAY, HASH and CODE) not supplied in the "use autobox"
       statement. As with the other options, the "value" corresponding to the
       "DEFAULT" "key" can be a class name, a namespace, or a reference to an
       array containing one or more class names and/or namespaces.

       Thus:

	   use autobox
	       STRING  =&gt 'MyString',
	       DEFAULT =&gt 'MyDefault';

       is equivalent to:

	   use autobox
	       STRING  =&gt 'MyString',
	       SCALAR  =&gt 'MyDefault',
	       ARRAY   =&gt 'MyDefault',
	       HASH    =&gt 'MyDefault',
	       CODE    =&gt 'MyDefault';

       Which in turn is equivalent to:

	   use autobox
	       INTEGER =&gt 'MyDefault',
	       FLOAT   =&gt 'MyDefault',
	       STRING  =&gt [ 'MyString', 'MyDefault' ],
	       ARRAY   =&gt 'MyDefault',
	       HASH    =&gt 'MyDefault',
	       CODE    =&gt 'MyDefault';

       Namespaces in DEFAULT values have the default type name appended,
       which, in the case of defaulted SCALAR types, is SCALAR rather than
       INTEGER, FLOAT &c.

       Thus:

	   use autobox
	       ARRAY   =&gt 'MyArray',
	       HASH    =&gt 'MyHash',
	       CODE    =&gt 'MyCode',
	       DEFAULT =&gt 'MyNamespace::';

       is equivalent to:

	   use autobox
	       INTEGER =&gt 'MyNamespace::SCALAR',
	       FLOAT   =&gt 'MyNamespace::SCALAR',
	       STRING  =&gt 'MyNamespace::SCALAR',
	       ARRAY   =&gt 'MyArray',
	       HASH    =&gt 'MyArray',
	       CODE    =&gt 'MyCode';

       Any of the four default types can be exempted from defaulting to the
       DEFAULT value by supplying a value of undef:

	   use autobox
	       HASH    =&gt undef,
	       DEFAULT =&gt 'MyDefault';

	   42-&gtfoo # ok: MyDefault::foo
	   []-&gtbar # ok: MyDefault::bar

	   %INC-&gtbaz # not ok: runtime error

   UNDEF
       The pseudotype, UNDEF, can be used to autobox undefined values. These
       are not autoboxed by default.

       This doesn't work:

	   use autobox;

	   undef-&gtfoo() # runtime error

       This works:

	   use autobox UNDEF =&gt 'MyUndef';

	   undef-&gtfoo(); # ok

       So does this:

	   use autobox UNDEF =&gt 'MyNamespace::';

	   undef-&gtfoo(); # ok

   NUMBER, SCALAR and UNIVERSAL
       The virtual types NUMBER, SCALAR and UNIVERSAL function as macros or
       shortcuts which create bindings for their subtypes. The type hierarchy
       is as follows:

	 UNIVERSAL -+
		    |
		    +- SCALAR -+
		    |	       |
		    |	       +- NUMBER -+
		    |	       |	  |
		    |	       |	  +- INTEGER
		    |	       |	  |
		    |	       |	  +- FLOAT
		    |	       |
		    |	       +- STRING
		    |
		    +- ARRAY
		    |
		    +- HASH
		    |
		    +- CODE

       Thus:

	   use autobox NUMBER =&gt 'MyNumber';

       is equivalent to:

	   use autobox
	       INTEGER =&gt 'MyNumber',
	       FLOAT   =&gt 'MyNumber';

       And:

	   use autobox SCALAR =&gt 'MyScalar';

       is equivalent to:

	   use autobox
	       INTEGER =&gt 'MyScalar',
	       FLOAT   =&gt 'MyScalar',
	       STRING  =&gt 'MyScalar';

       Virtual types can also be passed to "unimport" via the "no autobox"
       syntax. This disables autoboxing for the corresponding subtypes e.g.

	   no autobox qw(NUMBER);

       is equivalent to:

	   no autobox qw(INTEGER FLOAT);

       Virtual type bindings can be mixed with ordinary bindings to provide
       fine-grained control over inheritance and delegation. For instance:

	   use autobox
	       INTEGER =&gt 'MyInteger',
	       NUMBER  =&gt 'MyNumber',
	       SCALAR  =&gt 'MyScalar';

       would result in the following bindings:

	   42-&gtfoo	       -&gt [ MyInteger, MyNumber, MyScalar ]
	   3.1415927-&gtbar      -&gt [ MyNumber, MyScalar ]
	   "Hello, world!-&gtbaz -&gt [ MyScalar ]

       Note that DEFAULT bindings take precedence over virtual type bindings
       i.e.

	   use autobox
	       UNIVERSAL =&gt 'MyUniversal',
	       DEFAULT	 =&gt 'MyDefault'; # default SCALAR, ARRAY, HASH and CODE before UNIVERSAL

       is equivalent to:

	 use autobox
	     INTEGER =&gt [ 'MyDefault', 'MyUniversal' ],
	     FLOAT   =&gt [ 'MyDefault', 'MyUniversal' ], # ... &c.

   DEBUG
       "DEBUG" exposes the current bindings for the scope in which "use
       autobox" is called by means of a callback, or a static debugging
       function.

       This allows the computed bindings to be seen in "longhand".

       The option is ignored if the value corresponding to the "DEBUG" key is
       false.

       If the value is a CODE ref, then this sub is called with a reference to
       the hash containing the computed bindings for the current scope.

       Finally, if "DEBUG" is true but not a CODE ref, the bindings are dumped
       to STDERR.

       Thus:

	   use autobox DEBUG =&gt 1, ...

       or

	   use autobox DEBUG =&gt sub { ... }, ...

       or

	   sub my_callback ($) {
	       my $hashref = shift;
	       ...
	   }

	   use autobox DEBUG =&gt \&my_callback, ...

METHODS
   import
       On its own, "autobox" doesn't implement any methods that can be called
       on native types.  However, its static method, "import", can be used to
       implement "autobox" extensions i.e.  lexically scoped modules that
       provide "autobox" bindings for one or more native types without
       requiring calling code to "use autobox".

       This is done by subclassing "autobox" and overriding "import". This
       allows extensions to effectively translate "use MyModule" into a
       bespoke "use autobox" call. e.g.:

	   package String::Trim;

	   use base qw(autobox);

	   sub import {
	       my $class = shift;
	       $class-&gtSUPER::import(STRING =&gt 'String::Trim::Scalar');
	   }

	   package String::Trim::Scalar;

	   sub trim {
	       my $string = shift;
	       $string =~ s/^\s+//;
	       $string =~ s/\s+$//;
	       $string;
	   }

	   1;

       Note that "trim" is defined in an auxiliary class rather than in
       "String::Trim" itself to prevent "String::Trim"'s own methods (i.e. the
       methods it inherits from "autobox") being exposed to SCALAR types.

       This module can now be used without a "use autobox" statement to enable
       the "trim" method in the current lexical scope. e.g.:

	   #!/usr/bin/env perl

	   use String::Trim;

	   print "  Hello, world!  "-&gttrim();

EXPORTS
   autobox_class
       "autobox" makes a single method available to autoboxed types:
       "autobox_class". This can be used to call "isa", "can", "VERSION",
       "import" and "unimport". e.g.

	   if (42-&gtautobox_class-&gtisa('SCALAR')) ...
	   if (sub { ... }-&gtautobox_class-&gtcan('curry')) ...

   type
       "autobox" includes an additional module, "autobox::universal", which
       exports a single subroutine, "type".

       This sub returns the type of its argument within "autobox" (which is
       essentially longhand for the type names used within perl). This value
       is used by "autobox" to associate a method invocant with its designated
       classes. e.g.

	   use autobox::universal qw(type);

	   type("Hello, world!") # STRING
	   type(42)		 # INTEGER
	   type([])		 # ARRAY
	   type(sub { })	 # CODE

       "autobox::universal" is loaded automatically by "autobox", and, as its
       name suggests, can be used to install a universal method (i.e. a method
       for all "autobox" types) e.g.

	   use autobox UNIVERSAL =&gt 'autobox::universal';

	   42-&gttype	   # INTEGER
	   3.1415927-&gttype # FLOAT
	   %ENV-&gttype	   # HASH

CAVEATS
   Performance
       Autoboxing comes at a price. Calling

	   "Hello, world!"-&gtlength()

       is slightly slower than the equivalent method call on a string-like
       object, and significantly slower than

	   length("Hello, world!")

   Gotchas
       Precedence

       Due to Perl's precedence rules, some autoboxed literals may need to be
       parenthesized:

       For instance, while this works:

	   my $curried = sub { ... }-&gtcurry();

       this doesn't:

	   my $curried = \&foo-&gtcurry();

       The solution is to wrap the reference in parentheses:

	   my $curried = (\&foo)-&gtcurry();

       The same applies for signed integer and float literals:

	   # this works
	   my $range = 10-&gtto(1);

	   # this doesn't work
	   my $range = -10-&gtto(10);

	   # this works
	   my $range = (-10)-&gtto(10);

       print BLOCK

       Perl's special-casing for the "print BLOCK ..." syntax (see perlsub)
       means that "print { expression() } ..."	(where the curly brackets
       denote an anonymous HASH ref) may require some further disambiguation:

	   # this works (
	   print { foo =&gt 'bar' }-&gtfoo();

	   # and this
	   print { 'foo', 'bar' }-&gtfoo();

	   # and even this
	   print { 'foo', 'bar', @_ }-&gtfoo();

	   # but this doesn't
	   print { @_ }-&gtfoo() ? 1 : 0

       In the latter case, the solution is to supply something other than a
       HASH ref literal as the first argument to "print()":

	   # e.g.
	   print STDOUT { @_ }-&gtfoo() ? 1 : 0;

	   # or
	   my $hashref = { @_ };
	   print $hashref-&gtfoo() ? 1 : 0;

	   # or
	   print '', { @_ }-&gtfoo() ? 1 : 0;

	   # or
	   print '' . { @_ }-&gtfoo() ? 1 : 0;

	   # or even
	   { @_ }-&gtprint_if_foo(1, 0);

       eval EXPR

       Like most pragmas, autobox performs operations at compile time, and, as
       a result, runtime string "eval"s are not executed within its scope i.e.
       this doesn't work:

	   use autobox;

	   eval "42-&gtfoo";

       The workaround is to use autobox within the "eval" e.g.

	   eval &lt&lt'EOS';
	       use autobox;
	       42-&gtfoo();
	   EOS

       Note that the "eval BLOCK" form works as expected:

	   use autobox;

	   eval { 42-&gtfoo() }; # OK

VERSION
       2.75

SEE ALSO
       o   autobox::Core

       o   Moose::Autobox

       o   perl5i

       o   Scalar::Properties

AUTHOR
       chocolateboy &ltchocolate@cpan.org&gt

COPYRIGHT
       Copyright (c) 2003-2011, chocolateboy.

       This module is free software. It may be used, redistributed and/or
       modified under the same terms as Perl itself.



perl v5.12.4			  2011-07-21			    autobox(3)
    </pre></body>

</html>
